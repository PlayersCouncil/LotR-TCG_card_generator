'#####################################
'Standard cards 2.5 x 3.5 - 750 x 1050
'#####################################

INCLUDE = settings.txt

BORDER=ROUNDED,#000000,0.15
UNIT=INCH
PAGE=8.5,11,PORTRAIT,HV

IF= [card_size] = huge
    DPI=568
ELSEIF= [card_size] = double
    DPI=284
ELSEIF= [card_size] = original
    DPI=142
ELSE
    DPI=300
ENDIF

CARDSIZE=2.5,3.5

;;THREADS = 8

LINK= vset1.csv

[debug_image_path] = examples/

RECTANGLE=,0,0,100%,100%,#000000

;;FONT = LOTR_TCG, 24, , #000000
;;FONT = LOTR_TCG-Lore, 24, , #000000
;;FONT = LOTR_TCG-SmallCaps, 24, , #000000

FONTALIAS = , ON

;;TEXT = , [id], 0, 0, 100%, 20%
;;IMAGE = , JOIN([debug_image_path],[image_name]), 0, 0, 100%, 100%, 0, PN
;;GRID = , 0, 0, 100%, 100%, #FF77FF, 0.025, 10, 14




;; Insert the card portrait, since this is "beneath" everything else
<character_card_portrait> = 10.5%, 14.5%, 78.5%, 42%
IMAGE = , JOIN([card_image_path],/,[image_name]), <character_card_portrait>, 0, PN

;;RECTANGLE=,<card_portrait>,#00FFFF

[full_template_path] = JOIN([card_template_path],/,[card_size],_res_export/)
[icon_path] = JOIN([full_template_path], components/)
[positioned_icon_path] = JOIN([icon_path], positioned/)

[character_template] = JOIN([full_template_path],[culture],_character_,[card_size],.png)
[gollum_freeps_character_template] = JOIN([full_template_path],gollum_freeps_character_,[card_size],.png)
[gollum_shadow_character_template] = JOIN([full_template_path],gollum_shadow_character_,[card_size],.png)

[modifier_template] = JOIN([full_template_path],[culture],_modifier_,[card_size],.png)
[gollum_freeps_modifier_template] = JOIN([full_template_path],gollum_freeps_modifier_,[card_size],.png)
[gollum_shadow_modifier_template] = JOIN([full_template_path],gollum_shadow_modifier_,[card_size],.png)

[site_standard_template] = JOIN([full_template_path],site_standard_,[card_size],.png)
[site_sanctuary_template] = JOIN([full_template_path],site_sanctuary_,[card_size],.png)

;;<card_background> = 0, -2%, 100%, 104%
<card_background> = 0, 0, 100%, 100%
;;RECTANGLE=,<card_background>,#00FFFF

;; Figure out the card template and put it down to be drawn on by all future steps
IF= ([card_type] = "Companion") _OR_ ([card_type] = Minion) _OR_ ([card_type] = Ally) _OR_ ([card_type] = Follower)
    IF=[culture] = "gollum"
        IF=[tags] @ "shadow"
            IMAGE = , [gollum_shadow_character_template], <card_background>, 0, PN
        ELSE
            IMAGE = , [gollum_freeps_character_template], <card_background>, 0, PN
        ENDIF
    ELSE
        IMAGE = , [character_template], <card_background>, 0, PN
    ENDIF
ELSEIF= ([card_type] = Condition) _OR_ ([card_type] = Event) _OR_ ([card_type] = Possession) _OR_ ([card_type] = Artifact)
    IF=[culture] = "gollum"
        IF=[tags] @ "shadow"
            IMAGE = , [gollum_shadow_modifier_template], <card_background>, 0, PN
        ELSE
            IMAGE = , [gollum_freeps_modifier_template], <card_background>, 0, PN
        ENDIF
    ELSE
        IMAGE = , [modifier_template], <card_background>, 0, PN
    ENDIF
ELSEIF= ([card_type] = Site)
    IF= [tags] @ "sanctuary"
        IMAGE = , [site_sanctuary_template], <card_background>, 0, PN
    ELSE
        IMAGE = , [site_standard_template], <card_background>, 0, PN
    ENDIF
ELSE=
    RECTANGLE=,0,0,100%,100%,#9C2542
ENDIF

;;RECTANGLE=,<card_portrait>,#FF00FF


;;Twilight Cost
FONT = LOTR_TCG, 15, BT, [icon_text_color]
TEXT=,[twilight],6.3%,3.5%,10%,10%

;;Title
FONT = LOTR_TCG-SmallCaps, 10, BT, [game_text_color]
IF=([unique] = T) _OR_ ([unique] = Y)
    TEXT=,"•[title]",19.5%,1.7%,90%,10%,LEFT
ELSE
    TEXT=," [title]",19.5%,1.7%,90%,10%,LEFT
ENDIF

;; Card Type
IF= [card_type] = Ally

ELSE
    FONT = LOTR_TCG-SmallCaps, 8.9, BT, [game_text_color]
    TEXT=,"[card_type] • [card_subtype]",10%,55.3%,80%,10%,CENTER
ENDIF


;;Subtitle
FONT = LOTR_TCG-SmallCaps, 8, BT, [game_text_color]
TEXT=,[subtitle],21.6%,5.8%,90%,10%,LEFT


;;Strength

IF= [strength] <> ""
    IMAGE = , JOIN([positioned_icon_path],strength_position_,[card_size],.png), <card_background>, 0, PN
    FONT = LOTR_TCG, 14, BT, [icon_text_color]
    TEXT=,[strength],7.5%,62.5%,10%,10%
ENDIF

;;Vitality
IF= [vitality] <> ""
    IMAGE = , JOIN([positioned_icon_path],vitality_position_,[card_size],.png), <card_background>, 0, PN
    FONT = LOTR_TCG, 14, BT, [icon_text_color]
    TEXT=,[vitality],7.5%,74%,10%,10%
ENDIF

;; Signets
IF= [signet] <> ''[LORE]
    IMAGE = , JOIN([positioned_icon_path],signet_,[signet],_position_,[card_size],.png), <card_background>, 0, PN
ENDIF

;;TODO: Site Number, Resistance

;;Collector's Info
FONT = LOTR_TCG, 4.2, BT, [game_text_color]
TEXT=,JOIN([set_num]," ",[rarity]," ", [card_num]),84.7%,87.7%,10%,10%,CENTER







HTMLFONT=fnt,LOTR_TCG,7.59,,[game_text_color],LEFT
HTMLMARGINS=fnt,0,0,0,0,TOP,100

HTMLFONT=lore,LOTR_TCG-Lore,7.599,,[game_text_color],LEFT
HTMLMARGINS=lore,2%,0,0,0,,98

<game_text_area> = 23.5%,67%,63%,25%
[card_full_text] = JOIN([game_text],<br><lore>,[lore],</lore>)
;;[card_full_text] = REPLACE([card_full_text], "\34\", XXXX)

INCLUDE=game_text_replacements.txt
HTMLTEXT=,[card_full_text],<game_text_area>,,0,BE,100,fnt

;; Uncommment this to reveal the game text area
;;RECTANGLE=,<game_text_area>,#ff00ff




SAVE=,card_export/[title]_[subtitle].png,0,0,100%,100%,,,[card_mask_path]