'#####################################
'Standard cards 2.5 x 3.5 - 750 x 1050
'#####################################

INCLUDE = settings.txt


UNIT=INCH
PAGE=8.5,11,PORTRAIT,HV
FONTALIAS = , ON

LINKENCCSV= []


IF= [card_size] = huge
    DPI=568
ELSEIF= [card_size] = double
    DPI=284
ELSEIF= [card_size] = original
    DPI=142
ELSE
    DPI=300
ENDIF

CARDSIZE=2.5,3.5

;;THREADS = 8

LINK= vset1.csv

[debug_print] = False
[debug_image_path] = "X:\Projects\lotr-tcg\templates\new_templates\examples\"

;; If certain values are not filled in, we will use these values instead
[default_game_text_spacing] = 100
[default_lore_spacing] = 98
[default_game_text_width] = 69%
[default_site_game_text_width] = 74%
[default_character_title_font_size] = 10
[default_character_subtitle_font_size] = 8
[default_modifier_title_font_size] = 9
[default_modifier_subtitle_font_size] = 8
[default_site_title_font_size] = 9
[default_ring_title_font_size] = 10.5
[default_border_color] = #000000

;; Systematic clean-up
[template] = CASESTRING([template], L)
[unique] = CASESTRING([unique], U)
[culture] = CASESTRING([culture], L)
[card_type] = CASESTRING([card_type], F)
[tags] = CASESTRING([tags], L)

;; Some site processing to get us the non-block site number
[sitenum] = [site:1,1]

;; Starts with black background as our canvas
RECTANGLE=,0,0,100%,100%,#000000


;; Debug drawing options for various purposes.
;;TEXT = , [id], 0, 0, 100%, 20%
;;IMAGE = , JOIN([debug_image_path],[image_name]), 0, 0, 100%, 100%, 0, PN
;;GRID = , 0, 0, 100%, 100%, #FF77FF, 0.025, 10, 14


;; Translating the settings file variables into logical paths
[full_template_path] = JOIN([card_template_path],/,[card_size],_res_export/)
[icon_path] = JOIN([full_template_path], components/)
[positioned_icon_path] = JOIN([icon_path], positioned/)

[character_template] = JOIN([full_template_path],[culture],_character_,[card_size],.png)
[gollum_freeps_character_template] = JOIN([full_template_path],gollum_freeps_character_,[card_size],.png)
[gollum_shadow_character_template] = JOIN([full_template_path],gollum_shadow_character_,[card_size],.png)

[modifier_template] = JOIN([full_template_path],[culture],_modifier_,[card_size],.png)
[gollum_freeps_modifier_template] = JOIN([full_template_path],gollum_freeps_modifier_,[card_size],.png)
[gollum_shadow_modifier_template] = JOIN([full_template_path],gollum_shadow_modifier_,[card_size],.png)

[site_standard_template] = JOIN([full_template_path],site_standard_,[card_size],.png)
[site_sanctuary_template] = JOIN([full_template_path],site_sanctuary_,[card_size],.png)


;; Various named regions used in multiple places

;;<card_background> = 0, -2%, 100%, 104%
<card_background> = 0, 0, 100%, 100%
;;RECTANGLE=,<card_background>,#00FFFF

<character_card_portrait> = 10.5%, 14.5%, 78.5%, 42%
<modifier_card_portrait> = 21.2%, 11.9%, 69.7%, 37%
<site_card_portrait> = 12%, 10%, 64.2%, 79.4%
<full_card_portrait> = 4.3%,3.1%,91.4%,93.8%

;;RECTANGLE=,<card_portrait>,#00FFFF

<character_unique> = 19.5%, 4.2%, 3%, 5%
<character_title> = 21.5%, 4.7%, 58%, 4%
<character_subtitle> = 21.6%, 9.3%, 58%, 3%
<modifier_title> = 6.5%, 15.7%, 9.6%, 38%
<site_title> = 7.5%, 24.9%, 4.5%, 50%

<character_type_panel> = 15%,58.5%,70%,3.8%
<modifier_type_panel> = 25%,58.5%,65%,3.8%

<twilight_cost_icon> = 6.3%,3.5%,10%,10%
<site_twilight_cost_icon> = 7.5%,3.9%,10%,10%
<site_site_number_icon> = 8.4%,85.9%,10%,10%
<site_tower_site_number_icon> = 23.2%,85.9%,10%,10%
<site_king_site_number_icon> = 23.2%,85.9%,10%,10%
<strength_icon> = 7.5%,62.5%,10%,10%
<vitality_icon> = 7.5%,74%,10%,10%
<third_icon> = 7.5%,84.5%,10%,10%

<game_text> = 23.5%,67%,[game_text_width],25%
<game_text_default> = 23.5%,67%,[default_game_text_width],25%
<site_game_text> = 77.5%,13%,8.7%,[game_text_width]
<site_game_text_default> = 77.5%,12.5%,8.7%,[default_site_game_text_width]


;; Set up our fonts for when we use HTML rendering (which is most everywhere).
;; Line spacing can be defined per card in the CSV, so we integrate it here.

HTMLFONT=gametext,LOTR_TCG,7.4,D,[game_text_color],LEFT



IF= [title_font_size] <> ""
    HTMLFONT=chartitle,LOTR_TCG-SmallCaps,{title_font_size?§},BDZ,[game_text_color],LEFT
    HTMLFONT=modtitle,LOTR_TCG-SmallCaps,{title_font_size?§},BD,[game_text_color],RIGHT
    HTMLFONT=modtitletop,LOTR_TCG-SmallCaps,{title_font_size?§},BDZ,[game_text_color],RIGHT
    HTMLFONT=sitetitle,LOTR_TCG-SmallCaps,{title_font_size?§},BDZ,[game_text_color],CENTER
    HTMLFONT=ringtitle,LOTR_TCG-SmallCaps,{title_font_size?§},BDZ,[game_text_color],RIGHT
ELSE
    HTMLFONT=chartitle,LOTR_TCG-SmallCaps,[default_character_title_font_size],BDZ,[game_text_color],LEFT
    HTMLFONT=modtitle,LOTR_TCG-SmallCaps,[default_modifier_title_font_size],BD,[game_text_color],RIGHT
    HTMLFONT=modtitletop,LOTR_TCG-SmallCaps,[default_modifier_title_font_size],BDZ,[game_text_color],RIGHT
    HTMLFONT=sitetitle,LOTR_TCG-SmallCaps,[default_site_title_font_size],BDZ,[game_text_color],CENTER
    HTMLFONT=ringtitle,LOTR_TCG-SmallCaps,[default_ring_title_font_size],BDZ,[game_text_color],RIGHT
ENDIF

IF= [subtitle_font_size] <> ""
    HTMLFONT=charsubtitle,LOTR_TCG-SmallCaps,{subtitle_font_size?§},BDZ,[game_text_color],LEFT
    HTMLFONT=modsubtitle,LOTR_TCG-SmallCaps,{subtitle_font_size?§},BDZ,[game_text_color],RIGHT
ELSE
    HTMLFONT=charsubtitle,LOTR_TCG-SmallCaps,[default_character_subtitle_font_size],BDZ,[game_text_color],LEFT
    HTMLFONT=modsubtitle,LOTR_TCG-SmallCaps,[default_modifier_subtitle_font_size],BDZ,[game_text_color],RIGHT
ENDIF

HTMLFONT=typeplate,LOTR_TCG-SmallCaps,8.9,BD,[game_text_color],CENTER
HTMLFONT=typesymbol,LOTR_TCG,8.9,D,[game_text_color]
HTMLFONT=lore,LOTR_TCG-Lore,7.5,D,[game_text_color],LEFT
HTMLFONT=numbers,LOTR_TCG_Icons,14,D,[icon_text_color],CENTER,0%, 0.5%,0.03,#000000

HTMLMARGINS=chartitle,0,0,0,0,CENTER
HTMLMARGINS=charsubtitle,0,0,0,0,CENTER
HTMLMARGINS=modtitle,0,0,0,0,CENTER,94
HTMLMARGINS=modtitletop,0,0,0,0,TOP,94
HTMLMARGINS=modsubtitle,-1%,0,0,0,
HTMLMARGINS=sitetitle,0,0,0,0,CENTER
HTMLMARGINS=ringtitle,0,0,0,0,CENTER
HTMLMARGINS=numbers,0,0,0,0,CENTER
HTMLMARGINS=typeplate,0,0,0,0,CENTER

IF= [game_text_spacing] <> ""
    HTMLMARGINS=gametext,0,0,0,0,TOP,[game_text_spacing]
ELSE
    HTMLMARGINS=gametext,0,0,0,0,TOP,[default_game_text_spacing]
ENDIF

IF= [lore_spacing] <> ""
    HTMLMARGINS=lore,2%,0,0,0,,[lore_spacing]
ELSE
    HTMLMARGINS=lore,2%,0,0,0,,[default_lore_spacing]
ENDIF

;; including this file will perform a ton of find-replaces on all HTML-
;; displayed text, which is mostly used for obnoxious accents.
INCLUDE=all_text_replacements.txt

;; Now we get to card rendering.  Start with card-template-sensitive elements.
IF= [template] = "character"

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 0, PN
    ELSE
        ;; Insert the card portrait, since this is "beneath" everything else
        IMAGE = , JOIN([card_image_path],/,[image_name]), <character_card_portrait>, 0, PN

        ;; Now for the actual card template
        IF=[culture] = "gollum"
            ;; which template to use for gollum is stored in the [tags] column
            IF= "shadow" @ {tags?§}
                IMAGE = , [gollum_shadow_character_template], <card_background>, 0, PN
            ELSE
                IMAGE = , [gollum_freeps_character_template], <card_background>, 0, PN
            ENDIF
        ELSE
            IMAGE = , [character_template], <card_background>, 0, PN
        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        RECTANGLE=,<character_title>,#00FFFF
        RECTANGLE=,<character_subtitle>,#00FFFF
    ENDIF

    ;; Character Title
    IF=([unique] = T) _OR_ ([unique] = Y)
        HTMLTEXT=,"•",<character_unique>,,0,BE,100,chartitle
    ENDIF
    HTMLTEXT=,[title],<character_title>,,0,BE,100,chartitle
    

    ;; Character Subtitle
    IF= [subtitle] <> ""
        HTMLTEXT=,"[subtitle]",<character_subtitle>,,0,BE,100,charsubtitle
    ENDIF

    ;; Character Card Type
    IF= [card_type] = Ally
        IF= ([site] <> "") _AND_ ([card_subtype] <> "")
            HTMLTEXT=,"[card_type] • Home [site] • [card_subtype]",<character_type_panel>,,0,BE,100,typeplate
            ;;HTMLTEXT=,"[card_type] • Home [site] • [card_subtype]",15%,58.5%,80%,15%,,0,BEY,100,typeplate
        ELSEIF= [site] <> ""
            HTMLTEXT=,"[card_type] • Home [site]",<character_type_panel>,,0,BE,100,typeplate
        ELSEIF= [card_subtype] <> ""
            HTMLTEXT=,"[card_type] • [card_subtype]",<character_type_panel>,,0,BE,100,typeplate
        ELSE
            HTMLTEXT=,"[card_type]",<character_type_panel>,,0,BE,100,typeplate
        ENDIF
    ELSEIF= [card_subtype] = ""
        HTMLTEXT=,"[card_type]",<character_type_panel>,,0,BE,100,typeplate
    ELSE
        HTMLTEXT=,"[card_type] • [card_subtype]",<character_type_panel>,,0,BE,100,typeplate
    ENDIF

    ;; Minion Site Number
    IF= ([card_type] <> Ally) _AND_ ([site] <> "")
        IMAGE = , JOIN([positioned_icon_path],minion_site_number_position_,[card_size],.png), <card_background>, 0, PN
        HTMLTEXT=,"[site]",<third_icon>,,0,BE,100,numbers
    ENDIF

    ;; Resistance and ring-bearer
    IF= ([card_type] <> Minion) _AND_ ([resistance] <> "")
        IF= "ring-bearer" @ {tags?§}
            IMAGE = , JOIN([positioned_icon_path],resistance_ring_combined_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],resistance_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
        HTMLTEXT=,"[resistance]",<third_icon>,,0,BE,100,numbers
    ENDIF

ELSEIF= ([template] = "modifier") _OR_ ([template] = "support")

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 0, PN
    ELSE
        ;; Insert the card portrait, since this is "beneath" everything else
        IMAGE = , JOIN([card_image_path],/,[image_name]), <modifier_card_portrait>, 0, PN

        ;; Now for the actual card template
        IF=[culture] = "gollum"
            ;; which template to use for gollum is stored in the [tags] column
            IF="shadow" @ {tags?§}
                IMAGE = , [gollum_shadow_modifier_template], <card_background>, 0, PN
            ELSE
                IMAGE = , [gollum_freeps_modifier_template], <card_background>, 0, PN
            ENDIF
        ELSE
            IMAGE = , [modifier_template], <card_background>, 0, PN
        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        RECTANGLE=,<modifier_title>,#00FFFF
    ENDIF

    ;; Modifier Title/Subtitle
    IF= [subtitle] <> ""
        IF=([unique] = T) _OR_ ([unique] = Y)
            HTMLTEXT=,JOIN("•",[title],"<br><modsubtitle>",[subtitle],"</modsubtitle>"),<modifier_title>,,270,BE,100,modtitletop
        ELSE
            HTMLTEXT=,JOIN([title],"<br><modsubtitle>",[subtitle],"</modsubtitle>"),<modifier_title>,,270,BE,100,modtitletop
        ENDIF
    ELSE
        IF=([unique] = T) _OR_ ([unique] = Y)
            HTMLTEXT=,"•[title]",<modifier_title>,,270,BE,100,modtitle
        ELSE
            HTMLTEXT=,"[title]",<modifier_title>,,270,BE,100,modtitle
        ENDIF
    ENDIF

    ;; Card Type
    IF= [card_subtype] = ""
        HTMLTEXT=,"[card_type]",<modifier_type_panel>,,0,BE,100,typeplate
    ELSE
        HTMLTEXT=,"[card_type] • [card_subtype]",<modifier_type_panel>,,0,BE,100,typeplate
    ENDIF

ELSEIF= [template] = "site"

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 270, PN
    ELSE
        ;; Insert the card portrait, since this is "beneath" everything else
        IMAGE = , JOIN([card_image_path],/,[image_name]), <site_card_portrait>, 0, PN

        ;; Now for the actual card template
        IF= [card_type] = "Sanctuary"
            IMAGE = , [site_sanctuary_template], <card_background>, 270, PN
        ELSE
            IMAGE = , [site_standard_template], <card_background>, 270, PN
        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        RECTANGLE=,<site_title>,#00FFFF
    ENDIF

    ;; Site Title
    HTMLTEXT=,"[title]",<site_title>,,270,BE,100,sitetitle

    ;;RECTANGLE=,<site_card_portrait>,#FF00FF

    ;;RECTANGLE=,<site_twilight_cost_icon>,#ff00FF

    ;; Twilight Cost Icon
    IMAGE = , JOIN([positioned_icon_path],site_twilight_number_position_,[card_size],.png), <card_background>, 270, PN

    ;; Site Compass Icon / Site Number
    IF= "T" @ [site]
        IMAGE = , JOIN([positioned_icon_path],site_block_tower_position_,[card_size],.png), <card_background>, 270, PN
        HTMLTEXT=,"[sitenum]",<site_tower_site_number_icon>,,270,BE,100,numbers
    ELSEIF= "K" @ [site]
        IMAGE = , JOIN([positioned_icon_path],site_block_king_position_,[card_size],.png), <card_background>, 270, PN
        HTMLTEXT=,"[sitenum]",<site_king_site_number_icon>,,270,BE,100,numbers
    ELSEIF= [site] <> ""
        IMAGE = , JOIN([positioned_icon_path],site_block_fellowship_position_,[card_size],.png), <card_background>, 270, PN
        HTMLTEXT=,"[sitenum]",<site_site_number_icon>,,270,BE,100,numbers
    ELSE
        IMAGE = , JOIN([positioned_icon_path],site_block_shadows_position_,[card_size],.png), <card_background>, 270, PN
    ENDIF
    
    
    ;; Site Arrows
    IF= "right_arrow" @ {tags?§}
        IF= [card_type] = "Sanctuary"
            IMAGE = , JOIN([positioned_icon_path],site_arrow_right_sanctuary_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_arrow_right_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
        
    ELSE
        IF= [card_type] = "Sanctuary"
            IMAGE = , JOIN([positioned_icon_path],site_arrow_left_sanctuary_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_arrow_left_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
    ENDIF
    
ELSEIF= [template] = "onering"

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 0, PN
    ELSE
        ;; Insert the card portrait, since this is "beneath" everything else
        IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 0, PN

    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        RECTANGLE=,<modifier_title>,#00FFFF
    ENDIF

    

    ;; Title
    IF=([unique] = T) _OR_ ([unique] = Y)
        HTMLTEXT=,"•[title]",<modifier_title>,,270,BE,100,ringtitle
    ELSE
        HTMLTEXT=,"[title]",<modifier_title>,,270,BE,100,ringtitle
    ENDIF

    ;; Subtitle
    IF= [subtitle] <> ""
        HTMLTEXT=,"[subtitle]",<modifier_type_panel>,,0,BE,100,typeplate
    ENDIF

ELSE=
    RECTANGLE=,0,0,100%,100%,#9C2542
    TEXT=,"Unrecognized template type: [template]",0,0,100%,100%
ENDIF

;;RECTANGLE=,<modifier_type_panel>,#FF00FF




;; Twilight Cost
IF= ([template] = "character") _OR_ ([template] = "modifier") _OR_ ([template] = "support")
    HTMLTEXT=,"[twilight]",<twilight_cost_icon>,,0,BE,100,numbers
ELSEIF= [template] = "site"
    HTMLTEXT=,"[twilight]",<site_twilight_cost_icon>,,270,BE,100,numbers
ENDIF



;;Strength

IF= [strength] <> ""
    IF= "disable_strength" # {tags?§}
        IMAGE = , JOIN([positioned_icon_path],strength_position_,[card_size],.png), <card_background>, 0, PN
    ENDIF
    HTMLTEXT=,"[strength]",<strength_icon>,,0,BE,100,numbers
ENDIF

;;Vitality
IF= [vitality] <> ""
    IF= "disable_vitality" # {tags?§}
        IMAGE = , JOIN([positioned_icon_path],vitality_position_,[card_size],.png), <card_background>, 0, PN
    ENDIF
    HTMLTEXT=,"[vitality]",<vitality_icon>,,0,BE,100,numbers
ENDIF

;; Signets
IF= ([signet] <> "") _AND_ _NOT_ ("disable_signet" @ {tags?§})
    IMAGE = , JOIN([positioned_icon_path],signet_,[signet],_position_,[card_size],.png), <card_background>, 0, PN
ENDIF


;;Collector's Info
FONT = LOTR_TCG, 4.2, BT, [game_text_color]
TEXT=,JOIN([set_num]," ",[rarity]," ", [card_num]),84.7%,87.7%,10%,10%,CENTER


;; Debug drawing the game text area, before the drawing of the text so it remains visible
IF= [debug_regions] = "True"
    IF= [template] = "site"
        IF= [game_text_width] <> ""
            RECTANGLE=,<site_game_text>,#ff00ff
        ELSE
            RECTANGLE=,<site_game_text_default>,#ff00ff
        ENDIF
    ELSE
        IF= [game_text_width] <> ""
            RECTANGLE=,<game_text>,#ff00ff
        ELSE
            RECTANGLE=,<game_text_default>,#ff00ff
        ENDIF
    ENDIF
ENDIF


;; First combine the game text and lore into a single contiguous set of text
;; with the lore marked as a separate font.
[card_full_text] = JOIN([game_text],<br><lore>,[lore],</lore>)

;; including this file will perform a ton of find-replaces on the text, including
;; for symbols, bolding, and smart quote handling.
INCLUDE=game_text_replacements.txt

;; Finally, draw the game text itself.  Remember that if you alter the proportions
;; of the game text in these two clauses to also do it above in the debug draw.
;; This isn't stored in a frame because frames can't have their value switch on
;; CSV column data like HTMLTEXT can, so we're forced to copy-paste.
IF= ([template] = "character") _OR_ ([template] = "modifier") _OR_ ([template] = "support") _OR_ ([template] = "onering")
    IF= [game_text_width] <> ""
        HTMLTEXT=,[card_full_text],<game_text>,,0,BE,100,gametext
    ELSE
        HTMLTEXT=,[card_full_text],<game_text_default>,,0,BE,100,gametext
    ENDIF
ELSEIF = [template] = "site"
    IF= [game_text_width] <> ""
        HTMLTEXT=,[card_full_text],<site_game_text>,,270,BE,100,gametext
    ELSE
        HTMLTEXT=,[card_full_text],<site_game_text_default>,,270,BE,100,gametext
    ENDIF
ENDIF

;;GRID=,<full_card_portrait>,#FF00FF,0.01,11,15



IF= "disclaimer_light" @ {tags?§}
    IMAGE = , JOIN([icon_path],disclaimer_light_,[card_size],.png), 33.5%,94%,61%,2.7%, 0, PN
ELSEIF= ([template] = "onering") _OR_ ("disclaimer" @ {tags?§}) _OR_ ("full_art" @ {tags?§}) 
    IMAGE = , JOIN([icon_path],disclaimer_dark_,[card_size],.png), 33.5%,94%,61%,2.7%, 0, PN
ENDIF



;; Draw additional borders over the top
IF= {border_color?§} <> ""
    ROUNDRECT=,<card_background>,{border_color?§},EMPTY,9%,7,9.8
ELSE
    ROUNDRECT=,<card_background>,[default_border_color],EMPTY,9%,7,9.8
ENDIF


;; Last but not least, finally write the file (if not configured to do otherwise).
IF= [auto_write_files] = True
    IF= [include_size_in_card_output] = "True"
        SAVE=,JOIN([output_path],/,[id],_,[card_size],.png),0,0,100%,100%,,,[card_mask_path]
    ELSE
        SAVE=,JOIN([output_path],/,[id],.png),0,0,100%,100%,,,[card_mask_path]
    ENDIF
ENDIF

