'#####################################
'Standard cards 2.5 x 3.5 - 750 x 1050
'#####################################

INCLUDE= settings.txt
INCLUDE= macros.nde

UNIT=INCH
PAGE=8.5,11,PORTRAIT,HV
FONTALIAS = , ON
GAP=0.1,0.1,OFF,ON,MARKDOT,#FFFFFF

LINKENCCSV= []
IMAGEFILTER= LANCZOS

IF= [card_size] = huge
    DPI=568
    OVERSAMPLE = 1
ELSEIF= [card_size] = double
    DPI=284
    OVERSAMPLE = 1
ELSEIF= [card_size] = original
    DPI=142
    OVERSAMPLE = 2
ELSE
    DPI=300
    OVERSAMPLE = 2
ENDIF


CARDSIZE=2.5,3.5

;;THREADS = 8

[debug_print] = False
[debug_image_path] = "X:\Projects\lotr-tcg\csv_virtual_card_converter\card_portraits\"
;;Don't modify the following lines, they exist to make sure that these columns
;; can be omitted without causing validation errors.
[id] = ""
[image_name] = ""
[unique] = ""
[title] = ""
[subtitle] = ""
[culture] = ""
[template] = ""
[card_type] = ""
[display_type] = ""
[card_subtype] = ""
[twilight] = ""
[strength] = ""
[vitality] = ""
[resistance] = ""
[signet] = ""
[site] = ""
[set_num] = ""
[rarity] = ""
[card_num] = ""
[game_text] = ""
[lore] = ""
[promo_text] = ""
[custom_icon] = ""
[tags] = ""
[game_text_width] = ""
[game_text_color] = ""
[game_text_scale] = ""
[game_text_spacing] = ""
[lore_gap] = ""
[lore_spacing] = ""
[title_area_scale] = ""
[title_text_scale] = ""
[subtitle_text_scale] = ""
[title_color_override] = ""
[type_color_override] = ""
[type_text_scale] = ""
[icon_text_color] = ""
[twilight_color] = ""
[border_color] = ""
[errata_date] = ""
[notes] = ""
[debug_image_name] = ""


;;LINK= cards.csv
LINK= [spreadsheet_path]

;; Starts with white background as our canvas
RECTANGLE=,<card_background>,#FFFFFF
RECTANGLE=,<full_card_portrait>,#000000

;; Spacing and customizable width defaults
[default_game_text_spacing] = 98
[default_lore_gap] = 1.5%
[default_lore_spacing] = 98
[default_game_text_width] = 69%
[default_site_game_text_width] = 76.2%
[default_race_game_text_width] = 75%
[default_mod_title_text_width] = 38%


;; Title/Subtitle font sizes
[default_character_title_font_size] = {9.6 * [font_scale_factor]}
[custom_character_title_font_size] = {[default_character_title_font_size] * {title_text_scale?§}}
[default_character_subtitle_font_size] = {7.7 * [font_scale_factor]}
[custom_character_subtitle_font_size] = {[default_character_subtitle_font_size] * {subtitle_text_scale?§}}
[default_modifier_title_font_size] = {9 * [font_scale_factor]}
[custom_modifier_title_font_size] = {[default_modifier_title_font_size] * {title_text_scale?§}}
[default_modifier_subtitle_font_size] = {7.6 * [font_scale_factor]}
[custom_modifier_subtitle_font_size] = {[default_modifier_subtitle_font_size] * {subtitle_text_scale?§}}
[default_site_title_font_size] = {9 * [font_scale_factor]}
[custom_site_title_font_size] = {[default_site_title_font_size] * {title_text_scale?§}}
[default_ring_title_font_size] = {11 * [font_scale_factor]}
[custom_ring_title_font_size] = {[default_ring_title_font_size] * {title_text_scale?§}}

;; Icon and type plate font sizes
[default_icon_font_size] = {14* [font_scale_factor]}
[default_type_plate_font_size] = {8.8* [font_scale_factor]}
[custom_type_plate_font_size] = {[default_type_plate_font_size] * {type_text_scale?§}}
[default_type_symbol_font_size] = {8.9* [font_scale_factor]}
[custom_type_symbol_font_size] = {[default_type_symbol_font_size] * {type_text_scale?§}}


;; Small Caps font sizes
[title_cap_factor] = 1.2
[type_title_cap_factor] = 1.1
[mod_title_cap_factor] = 1.15
[promo_cap_factor] = 1.2

[default_char_title_cap_font_size] = {[default_character_title_font_size] * [title_cap_factor]}
[custom_char_title_cap_font_size] = {[default_char_title_cap_font_size] * {title_text_scale?§}}
[default_char_subtitle_cap_font_size] = {[default_character_subtitle_font_size] * [title_cap_factor]}
[custom_char_subtitle_cap_font_size] = {[default_char_subtitle_cap_font_size] * {subtitle_text_scale?§}}

[default_mod_title_cap_font_size] = {[default_modifier_title_font_size] * [mod_title_cap_factor]}
[custom_mod_title_cap_font_size] = {[default_mod_title_cap_font_size] * {title_text_scale?§}}
[default_mod_subtitle_cap_font_size] = {[default_modifier_subtitle_font_size] * [mod_title_cap_factor]}
[custom_mod_subtitle_cap_font_size] = {[default_mod_subtitle_cap_font_size] * {subtitle_text_scale?§}}

[default_ring_title_cap_font_size] = {[default_ring_title_font_size] * [mod_title_cap_factor]}
[custom_ring_title_cap_font_size] = {[default_ring_title_cap_font_size] * {title_text_scale?§}}

[default_type_plate_cap_font_size] = {[default_type_plate_font_size] * [type_title_cap_factor]}
[custom_type_plate_cap_font_size] = {[default_type_plate_cap_font_size] * {type_text_scale?§}}


;; All other font sizes
[default_game_text_font_size] = {7.4* [font_scale_factor]}
[custom_game_text_font_size] = {[default_game_text_font_size] * {game_text_scale?§}}
[default_text_symbol_font_size] = {7.4* [font_scale_factor]}
[custom_text_symbol_font_size] = {[default_text_symbol_font_size] * {game_text_scale?§}}
[default_helper_text_font_size] = {7.25* [font_scale_factor]}
[custom_helper_text_font_size] = {[default_helper_text_font_size] * {game_text_scale?§}}
[default_lore_font_size] = {7.5* [font_scale_factor]}
[custom_lore_font_size] = {[default_lore_font_size] * {game_text_scale?§}}
[default_promo_font_size] = {7.1* [font_scale_factor]}
[custom_promo_font_size] = {[default_promo_font_size] * {game_text_scale?§}}
[default_promo_cap_font_size] = {[default_promo_font_size] * [promo_cap_factor]}
[custom_promo_cap_font_size] = {[default_promo_cap_font_size] * {game_text_scale?§}}

[custom_lore_spacing] = {[default_lore_spacing] * {lore_spacing?§}}
[custom_game_text_spacing] = {[default_game_text_spacing] * {game_text_spacing?§}}
[custom_lore_gap] = {[default_lore_gap] * {lore_gap?§}}

[default_coll_info_font_size] = {3.7* [font_scale_factor]}
[default_coll_symbol_font_size] = {3.2* [font_scale_factor]}
[default_coll_plus_font_size] = {4.75* [font_scale_factor]}

[default_errata_font_size] = 7








;;{default_character_title_font_size * char_title_cap_factor}

;;[default_char_title_cap_font_size] = {[default_character_title_font_size] * [char_title_cap_factor]}



[default_border_color] = #000000



;; Systematic clean-up

[template] = CASESTRING([template], L)
[unique] = CASESTRING([unique], U)
[culture] = CASESTRING([culture], L)
[culture] = REPLACE([culture], "elf", "elven")
[culture] = REPLACE([culture], "dwarf", "dwarven")
[card_type] = CASESTRING([card_type], F)
[tags] = CASESTRING([tags], L)
[tags] = REPLACE([tags], "no_template", "full_art")
[tags] = REPLACE([tags], "portrait_backdrop", "full_portrait")


;; Some site processing to get us the non-block site number

;[first]=[site:1,1]
;[hasregion]="R1" @ [site]
;
;[sitenum] = [first]
;[sitenum]%[hasregion] = [site:1,2]
[siteone] = [site:1,1]
[sitetwo] = [site:1,2]





;; Debug drawing options for various purposes.
;;TEXT = , [id], 0, 0, 100%, 20%
;;IMAGE = , JOIN([debug_image_path],[image_name]), 0, 0, 100%, 100%, 0, PN
;;GRID = , 0, 0, 100%, 100%, #FF77FF, 0.025, 10, 14


;; Translating the settings file variables into logical paths
[full_template_path] = JOIN([card_template_path],/,[card_size],_res_export/)
[icon_path] = JOIN([full_template_path], components/)
[positioned_icon_path] = JOIN([icon_path], positioned/)
[mockup_positioned_icon_path] = JOIN([positioned_icon_path], mockups/)
[overlay_path] = JOIN([full_template_path], overlays/)
[mockup_overlay_path] = JOIN([overlay_path], mockups/)

[character_template] = JOIN([full_template_path],[culture],_character_,[card_size],.png)
[mockup_character_template] = JOIN([full_template_path],mockups/,[culture],_character_mockup_,[card_size],.png)
[modifier_template] = JOIN([full_template_path],[culture],_modifier_,[card_size],.png)
[mockup_modifier_template] = JOIN([full_template_path],mockups/,[culture],_modifier_mockup_,[card_size],.png)
[back_template] = JOIN([full_template_path],card_back_,[card_size],.png)

[site_standard_template] = JOIN([full_template_path],site_standard_,[card_size],.png)
[mockup_site_standard_template] = JOIN([full_template_path],mockups/,site_standard_mockup_,[card_size],.png)
[site_sanctuary_template] = JOIN([full_template_path],site_sanctuary_,[card_size],.png)
[mockup_site_sanctuary_template] = JOIN([full_template_path],mockups/,site_sanctuary_mockup_,[card_size],.png)
[custom_site_standard_template] = JOIN([full_template_path],[culture],_standard_,[card_size],.png)
[custom_site_sanctuary_template] = JOIN([full_template_path],[culture],_sanctuary_,[card_size],.png)

[site_race_template] = JOIN([full_template_path],site_race_,[card_size],.png)
[mockup_site_race_template] = JOIN([full_template_path],mockups/,site_race_mockup_,[card_size],.png)
[custom_site_race_template] = JOIN([full_template_path],[culture],_race_,[card_size],.png)




[artifact_overlay] = JOIN([overlay_path],artifact_,[culture],_,[card_size],.png)
[mockup_artifact_overlay] = JOIN([mockup_overlay_path],artifact_,[culture],_mockup_,[card_size],.png)
[culture_overlay] = JOIN([overlay_path],culture_,[culture],_,[card_size],.png)
[mockup_culture_overlay] = JOIN([mockup_overlay_path],culture_,[culture],_mockup_,[card_size],.png)
[freeps_twilight_overlay] = JOIN([overlay_path],twilight_freeps_,[card_size],.png)
[mockup_freeps_twilight_overlay] = JOIN([mockup_overlay_path],twilight_freeps_mockup_,[card_size],.png)
[shadow_twilight_overlay] = JOIN([overlay_path],twilight_shadow_,[card_size],.png)
[mockup_shadow_twilight_overlay] = JOIN([mockup_overlay_path],twilight_shadow_mockup_,[card_size],.png)
[custom_icon_path] = JOIN([positioned_icon_path],[custom_icon],_position_,[card_size],.png)


;; Various named regions used in multiple places

;;<card_background> = 0, -2%, 100%, 104%
<card_background> = 0, 0, 100%, 100%
;;RECTANGLE=,<card_background>,#00FFFF

<character_card_portrait> = 10.5%, 14.5%, 78.5%, 42%
<modifier_card_portrait> = 21.2%, 11.9%, 69.7%, 37%
<site_card_portrait> = 12%, 10%, 64.2%, 79.4%
<race_card_portrait> = 14.6%, 8.6%, 70.6%, 64.1%
<full_card_portrait> = 4.3%,3.1%,91.4%,93.8%

;;RECTANGLE=,<card_portrait>,#00FFFF

<character_unique> = 19.5%, 4.2%, 3%, 5%
<character_title> = 19.5%, 4.6%, 58%, 4%
<character_subtitle> = 19.6%, 8.4%, 58%, 4%
<modifier_title> = 6.5%, 15.7%, 9.6%, {{title_area_scale?§} * [default_mod_title_text_width]}
<modifier_title_default> = 6.5%, 15.7%, 9.6%, [default_mod_title_text_width]
<site_title> = 7.5%, 24.9%, 4.5%, 50%
<race_title> = 21%, 6.5%, 57%, 3%

<character_type_panel> = 15%,58.4%,70%,3.8%
<modifier_type_panel> = 25%,58.5%,65%,3.8%

<twilight_cost_icon> = 6.3%,3.5%,10%,10%
<site_twilight_cost_icon> = 7.5%,3.9%,10%,10%
<site_site_number_icon> = 8.4%,85.9%,10%,10%
<site_tower_site_number_icon> = 23.2%,85.9%,10%,10%
<site_king_site_number_icon> = 23.2%,85.9%,10%,10%
<race_region_icon> = 9%,32%,10%,5%

<strength_icon> = 7.5%,62.5%,10%,10%
<vitality_icon> = 7.5%,74%,10%,10%
<third_icon> = 7.5%,84.5%,10%,10%
<disclaimer_bar> = 33.5%,94%,61%,2.7%

<game_text> = 22.8%,66.4%,{{game_text_width?§} * [default_game_text_width]},26.5%
<game_text_default> = 22.8%,66.4%,[default_game_text_width],26.5%
<site_game_text> = 77.5%,11.8%,9%,{{game_text_width?§} * [default_site_game_text_width]}
<site_game_text_default> = 77.5%,11.8%,9%,[default_site_game_text_width]
<race_game_text> = 12.5%,75%,{{game_text_width?§} * [default_race_game_text_width]},16%
<race_game_text_default> = 12.8%,75.5%,[default_race_game_text_width],16%

<collectors_info> = 84.8%,91.8%,10%,2%


;; Set up our fonts for when we use HTML rendering (which is most everywhere).
;; Line spacing can be defined per card in the CSV, so we integrate it here.

SETFONT= hiddendot,[title_font],[custom_character_title_font_size],[default_character_title_font_size],DZA,#00000000,#00000000,#00000000,
SETFONT= chartitle,[title_font],[custom_character_title_font_size],[default_character_title_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§},LEFT
SETFONT= chartitlecap,[title_font],[custom_char_title_cap_font_size],[default_char_title_cap_font_size],D,{game_text_color?§},[default_game_text_color],{title_color_override?§}
SETFONT= charsubtitle,[title_font],[custom_character_subtitle_font_size],[default_character_subtitle_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§},LEFT
SETFONT= charsubtitlecap,[title_font],[custom_char_subtitle_cap_font_size],[default_char_subtitle_cap_font_size],D,{game_text_color?§},[default_game_text_color],{title_color_override?§}

SETFONT= modtitle,[title_font],[custom_modifier_title_font_size],[default_modifier_title_font_size],DA,{game_text_color?§},[default_game_text_color],{title_color_override?§},RIGHT
;; this is doubled just for the margins
SETFONT= modtitletop,[title_font],[custom_modifier_title_font_size],[default_modifier_title_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§},RIGHT
SETFONT= modtitlecap,[title_font],[custom_mod_title_cap_font_size],[default_mod_title_cap_font_size],D,{game_text_color?§},[default_game_text_color],{title_color_override?§}

SETFONT= modsubtitle,[title_font],[custom_modifier_subtitle_font_size],[default_modifier_subtitle_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§},RIGHT
SETFONT= modsubtitlecap,[title_font],[custom_mod_subtitle_cap_font_size],[default_mod_subtitle_cap_font_size],D,{game_text_color?§},[default_game_text_color],{title_color_override?§}

SETFONT= sitetitle,[title_font],[custom_site_title_font_size],[default_site_title_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§},CENTER
SETFONT= ringtitle,[title_font],[custom_ring_title_font_size],[default_ring_title_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§},RIGHT
SETFONT= ringtitlecap,[title_font],[custom_ring_title_cap_font_size],[default_ring_title_cap_font_size],DZA,{game_text_color?§},[default_game_text_color],{title_color_override?§}

SETFONT= typeplate,[title_font],[custom_type_plate_font_size],[default_type_plate_font_size],DZA,{game_text_color?§},[default_game_text_color],{type_color_override?§},CENTER
SETFONT= typeplatecap,[title_font],[custom_type_plate_cap_font_size],[default_type_plate_cap_font_size],DZA,{game_text_color?§},[default_game_text_color],{type_color_override?§}
SETFONT= typesymbol,[game_text_font],[custom_type_symbol_font_size],[default_type_symbol_font_size],D,{game_text_color?§},[default_game_text_color],{type_color_override?§}

SETFONT= gametext,[game_text_font],[custom_game_text_font_size],[default_game_text_font_size],D,{game_text_color?§},[default_game_text_color],,LEFT
SETFONT= thin,[game_text_thin_font],[custom_game_text_font_size],[default_game_text_font_size],D,{game_text_color?§},[default_game_text_color],,LEFT
SETFONT= textsymbol,[game_text_font],[custom_text_symbol_font_size],[default_text_symbol_font_size],D,{game_text_color?§},[default_game_text_color]
SETFONT= helper,[game_text_font],[custom_helper_text_font_size],[default_helper_text_font_size],DI,{game_text_color?§},[default_game_text_color]

IF= "thinlore" @ {tags?§}
    SETFONT= lore,[lore_thin_font],[custom_lore_font_size],[default_lore_font_size],D,{game_text_color?§},[default_game_text_color],,LEFT
ELSE
    SETFONT= lore,[lore_font],[custom_lore_font_size],[default_lore_font_size],D,{game_text_color?§},[default_game_text_color],,LEFT
ENDIF

SETFONT= promo,[promo_font],[custom_promo_font_size],[default_promo_font_size],BDAR,{game_text_color?§},[default_game_text_color],,CENTER
SETFONT= promocap,[promo_font],[custom_promo_cap_font_size],[default_promo_cap_font_size],BDAR,{game_text_color?§},[default_game_text_color]

SETFONT= collinfo, [number_font],,[default_coll_info_font_size],D, {game_text_color?§},[default_game_text_color],,CENTER
SETFONT= colltext, [game_text_font],,[default_coll_symbol_font_size],D, {game_text_color?§},[default_game_text_color]
SETFONT= collplus, [game_text_font],,[default_coll_plus_font_size],D, {game_text_color?§},[default_game_text_color]

HTMLFONT=erratatext,[number_font],[default_errata_font_size],D,[default_icon_text_color],CENTER,0%, 0.5%,0.03,#000000


IF= {icon_text_color?§} <> ""
    HTMLFONT=numbers,[number_font],[default_icon_font_size],D,{icon_text_color?§},CENTER,0%, 0.5%,0.03,#000000

    IF= {twilight_color?§} <> ""
        HTMLFONT=twilight,[number_font],[default_icon_font_size],D,{twilight_color?§},CENTER,0%, 0.5%,0.03,#000000
    ELSE
        HTMLFONT=twilight,[number_font],[default_icon_font_size],D,{icon_text_color?§},CENTER,0%, 0.5%,0.03,#000000
    ENDIF
ELSE
    HTMLFONT=numbers,[number_font],[default_icon_font_size],D,[default_icon_text_color],CENTER,0%, 0.5%,0.03,#000000

    IF= {twilight_color?§} <> ""
        HTMLFONT=twilight,[number_font],[default_icon_font_size],D,{twilight_color?§},CENTER,0%, 0.5%,0.03,#000000
    ELSE
        HTMLFONT=twilight,[number_font],[default_icon_font_size],D,[default_icon_text_color],CENTER,0%, 0.5%,0.03,#000000
    ENDIF
ENDIF

HTMLMARGINS=chartitle,0,0,0,0,CENTER
HTMLMARGINS=charsubtitle,0,0,0,0,CENTER
HTMLMARGINS=modtitle,0,0,0,0,CENTER,94
HTMLMARGINS=modtitletop,0,0,0,0,TOP,94
HTMLMARGINS=modsubtitle,-1%,0,0,0,
HTMLMARGINS=sitetitle,0,0,0,0,CENTER
HTMLMARGINS=ringtitle,0,0,0,0,CENTER
HTMLMARGINS=twilight,0,0,0,0,CENTER
HTMLMARGINS=numbers,0,0,0,0,CENTER
HTMLMARGINS=typeplate,0,0,0,0,CENTER
HTMLMARGINS=promo,2.0%,0,0,0
HTMLMARGINS=typesymbol,0,0,0,0,,[default_game_text_spacing]
HTMLMARGINS=collplus,0,0,0,0,

IF= [game_text_spacing] <> ""
    HTMLMARGINS=gametext,0,0,0,0,,[game_text_spacing]
    HTMLMARGINS=thin,0,0,0,0,,[game_text_spacing]
    HTMLMARGINS=textsymbol,0,0,0,0,,[game_text_spacing]
    HTMLMARGINS=helper,0,0,0,0,,[game_text_spacing]
ELSE
    HTMLMARGINS=gametext,0,0,0,0,,[default_game_text_spacing]
    HTMLMARGINS=thin,0,0,0,0,,[default_game_text_spacing]
    HTMLMARGINS=textsymbol,0,0,0,0,,[default_game_text_spacing]
    HTMLMARGINS=helper,0,0,0,0,,[default_game_text_spacing]
ENDIF

IF= {lore_gap?§} <> ""
    IF= {lore_spacing?§} <> ""
        HTMLMARGINS=lore,[custom_lore_gap],0,0,0,,[lore_spacing]
    ELSE
        HTMLMARGINS=lore,[custom_lore_gap],0,0,0,,[default_lore_spacing]
    ENDIF
ELSE
    IF= {lore_spacing?§} <> ""
        HTMLMARGINS=lore,[default_lore_gap],0,0,0,,[lore_spacing]
    ELSE
        HTMLMARGINS=lore,[default_lore_gap],0,0,0,,[default_lore_spacing]
    ENDIF
ENDIF


;; including this file will perform a ton of find-replaces on all HTML-
;; displayed text, which is mostly used for obnoxious accents.
INCLUDE=all_text_replacements.txt

;; These text replacements have to be performed now, because for whatever reason
;; we cannot set variables within if statements.


[title] = REPLACE([title], [Text_Find], [Text_Replace])
[subtitle] = REPLACE([subtitle], [Text_Find], [Text_Replace])
[card_type] = REPLACE([card_type], [Text_Find], [Text_Replace])
[card_subtype] = REPLACE([card_subtype], [Text_Find], [Text_Replace])
[game_text] = REPLACE([game_text], [Text_Find], [Text_Replace])
[lore] = REPLACE([lore], [Text_Find], [Text_Replace])
[promo_text] = REPLACE([promo_text], "<BR>", "<br>")
[promo_text] = REPLACE([promo_text], "<bR>", "<br>")
[promo_text] = REPLACE([promo_text], "<Br>", "<br>")

[symrarity] = [rarity]
;;{rarity?§}
;;SETRING({rarity?§})
[symrarity] = REPLACE([symrarity], o, "<colltext>&#9675</colltext>")
[symrarity] = REPLACE([symrarity], O, "<colltext>&#9675</colltext>")
[symrarity] = REPLACE([symrarity], "+", "<collplus>+</collplus>")


;;HTMLTEXT=,"10[symrarity]R+",<character_card_portrait>,#FFFF00,0,BE,100,colltext


;;HTMLTEXT=,"[site]",<third_icon>,,0,BE,100,gametext
[home_site_formatted] = [site]

;; Site block replacements
[home_site_formatted] = REPLACE([home_site_formatted], "(T)","<typesymbol>`<typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "\91\T\93\", "<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "1T", "1<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "2T", "2<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "3T", "3<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "4T", "4<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "5T", "5<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "6T", "6<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "7T", "7<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "8T", "8<typesymbol>`</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "9T", "9<typesymbol>`</typesymbol>")

[home_site_formatted] = REPLACE([home_site_formatted], "(K)", "<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "\91\K\93\", "<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "1K", "1<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "2K", "2<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "3K", "3<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "4K", "4<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "5K", "5<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "6K", "6<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "7K", "7<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "8K", "8<typesymbol>~</typesymbol>")
[home_site_formatted] = REPLACE([home_site_formatted], "9K", "9<typesymbol>~</typesymbol>")

HTMLTEXT=,"[home_site_formatted]",<third_icon>,#FFFF00,0,BE,100,collinfo

;; Next we detect capital letters and html-tag them for cases where we want
;; customizable small-caps

TAGCAPS= "titlecaps_char","title","chartitlecap"
TAGCAPS= "subtitlecaps_char","subtitle","charsubtitlecap"

TAGCAPS= "titlecaps_mod","title","modtitlecap"
TAGCAPS= "subtitlecaps_mod","subtitle","modsubtitlecap"

TAGCAPS= "titlecaps_site","title","sitetitlecap"
TAGCAPS= "titlecaps_ring","title","ringtitlecap"
TAGCAPS= "subtitlecaps_ring","subtitle","typeplatecap"

TAGCAPS= "cardtypecaps","card_type","typeplatecap"
TAGCAPS= "carddisplaytypecaps","display_type","typeplatecap"
TAGCAPS= "cardsubtypecaps","card_subtype","typeplatecap"

TAGCAPS= "promocaps", "promo_text", "promocap"

TAGCAPS= "homecaps", "ally_home_label", "typeplatecap"




;; Now we get to card rendering.  Start with card-template-sensitive elements.
IF= {template?§} = "character"

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 0, PN
    ELSE
        ;; Full art means we are not using a template
        IF= "full_art" @ {tags?§}
            IF= "full_portrait" @ {tags?§}
                IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 0, PN
            ENDIF
        ELSE
            ;; Insert the card portrait, since this is "beneath" everything else

            ;; full portraits are intended to take up the whole card size, so
            ;; that lower rest cards can just be slotted in without needing to
            ;; cut out the portrait.
            IF= "full_portrait" @ {tags?§}
                IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([card_image_path],/,[image_name]), <character_card_portrait>, 0, PN
            ENDIF

            ;; Now for the actual card template
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , [mockup_character_template], <card_background>, 0, PN
            ELSE
                IMAGE = , [character_template], <card_background>, 0, PN
            ENDIF

        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        ;;RECTANGLE=,<character_unique>,#00FF00
        RECTANGLE=,<character_title>,#00FFFF

        RECTANGLE=,<character_subtitle>,#00FFFF
    ENDIF

    IF=([unique] = T) _OR_ ([unique] = Y)
        HTMLTEXT=,CONCAT1("<chartitlecap>•</chartitlecap>",{titlecaps_char?§}," "),<character_title>,,0,BE,100,chartitle
        HTMLTEXT=,CONCAT1("<chartitlecap>&nbsp;</chartitlecap>",{subtitlecaps_char?§}," "),<character_subtitle>,,0,BE,100,charsubtitle
    ELSE
        HTMLTEXT=,CONCAT1("<chartitlecap>&nbsp;</chartitlecap>"{titlecaps_char?§}," "),<character_title>,,0,BE,100,chartitle
        HTMLTEXT=,CONCAT1("<chartitlecap>&nbsp;</chartitlecap>",{subtitlecaps_char?§}," "),<character_subtitle>,,0,BE,100,charsubtitle
    ENDIF

    ;; Character Card Type
    IF= [card_type] = Ally
        IF= ([home_site_formatted] <> "") _AND_ ([card_subtype] <> "")
            IF= [display_type] <> ""
                HTMLTEXT=,CONCAT1({carddisplaytypecaps?§}," • ",{homecaps?1}," ",{home_site_formatted?§}," • ",{cardsubtypecaps?§}),<character_type_panel>,,0,BE,100,typeplate
            ELSE=
                HTMLTEXT=,CONCAT1({cardtypecaps?§}," • ",{homecaps?1}," ",{home_site_formatted?§}," • ",{cardsubtypecaps?§}),<character_type_panel>,,0,BE,100,typeplate
            ENDIF
        ELSEIF= [home_site_formatted] <> ""
            IF= [display_type] <> ""
                HTMLTEXT=,CONCAT1({carddisplaytypecaps?§}," • ",{homecaps?1}," ",{home_site_formatted?§}),<character_type_panel>,,0,BE,100,typeplate
            ELSE=
                HTMLTEXT=,CONCAT1({cardtypecaps?§}," • ",{homecaps?1}," ",{home_site_formatted?§}),<character_type_panel>,,0,BE,100,typeplate
            ENDIF
        ELSEIF= [card_subtype] <> ""
            IF= [display_type] <> ""
                HTMLTEXT=,CONCAT1({carddisplaytypecaps?§}," • ",{cardsubtypecaps?§}),<character_type_panel>,,0,BE,100,typeplate
            ELSE=
                HTMLTEXT=,CONCAT1({cardtypecaps?§}," • ",{cardsubtypecaps?§}),<character_type_panel>,,0,BE,100,typeplate
            ENDIF
        ELSE
            IF= [display_type] <> ""
                HTMLTEXT=,{{carddisplaytypecaps?§}},<character_type_panel>,,0,BE,100,typeplate
            ELSE=
                HTMLTEXT=,{{cardtypecaps?§}},<character_type_panel>,,0,BE,100,typeplate
            ENDIF
        ENDIF
    ELSEIF= [card_subtype] = ""
        IF= [display_type] <> ""
            HTMLTEXT=,{{carddisplaytypecaps?§}},<character_type_panel>,,0,BE,100,typeplate
        ELSE=
            HTMLTEXT=,{{cardtypecaps?§}},<character_type_panel>,,0,BE,100,typeplate
        ENDIF

    ELSE
        IF= [display_type] <> ""
            HTMLTEXT=,CONCAT1({carddisplaytypecaps?§}," • ",{cardsubtypecaps?§}),<character_type_panel>,,0,BE,100,typeplate
        ELSE=
            HTMLTEXT=,CONCAT1({cardtypecaps?§}," • ",{cardsubtypecaps?§}),<character_type_panel>,,0,BE,100,typeplate
        ENDIF
    ENDIF

    ;; Minion Site Number
    IF= ([card_type] <> Ally) _AND_ ([site] <> "")
        IF= "disable_site" # {tags?§}
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path],minion_site_number_mockup_position_,[card_size],.png), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],minion_site_number_position_,[card_size],.png), <card_background>, 0, PN
            ENDIF
        ENDIF
        HTMLTEXT=,"[site]",<third_icon>,,0,BE,100,numbers
    ENDIF



ELSEIF= ({template?§} = "modifier") _OR_ ({template?§} = "support")

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 0, PN
    ELSE
        ;; Full art means we are not using a template
        IF= "full_art" @ {tags?§}
            IF= "full_portrait" @ {tags?§}
                IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 0, PN
            ENDIF
        ELSE
            ;; Insert the card portrait, since this is "beneath" everything else

            ;; full portraits are intended to take up the whole card size, so
            ;; that lower rest cards can just be slotted in without needing to
            ;; cut out the portrait.
            IF= "full_portrait" @ {tags?§}
                IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([card_image_path],/,[image_name]), <modifier_card_portrait>, 0, PN
            ENDIF

            ;; Now for the actual card template
            
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , [mockup_modifier_template], <card_background>, 0, PN
            ELSE
                IMAGE = , [modifier_template], <card_background>, 0, PN
            ENDIF
        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        IF= [title_area_scale] <> ""
            RECTANGLE=,<modifier_title>,#00FFFF
        ELSE
            RECTANGLE=,<modifier_title_default>,#00FFFF
        ENDIF
    ENDIF

    

    ;; Modifier Title/Subtitle
    IF= [subtitle] <> ""
        IF=([unique] = T) _OR_ ([unique] = Y)
            IF= [title_area_scale] <> ""
                HTMLTEXT=,JOIN("•",[titlecaps_mod],"<br><modsubtitle>",[subtitlecaps_mod],"</modsubtitle>"),<modifier_title>,,270,BE,100,modtitletop
            ELSE
                HTMLTEXT=,JOIN("•",[titlecaps_mod],"<br><modsubtitle>",[subtitlecaps_mod],"</modsubtitle>"),<modifier_title_default>,,270,BE,100,modtitletop
            ENDIF
        ELSE
            IF= [title_area_scale] <> ""
                HTMLTEXT=,JOIN([titlecaps_mod],"<br><modsubtitle>",[subtitlecaps_mod],"</modsubtitle>"),<modifier_title>,,270,BE,100,modtitletop
            ELSE
                HTMLTEXT=,JOIN([titlecaps_mod],"<br><modsubtitle>",[subtitlecaps_mod],"</modsubtitle>"),<modifier_title_default>,,270,BE,100,modtitletop
            ENDIF
        ENDIF
    ELSE
        IF=([unique] = T) _OR_ ([unique] = Y)
            IF= [title_area_scale] <> ""
                HTMLTEXT=,JOIN("•",[titlecaps_mod]),<modifier_title>,,270,BE,100,modtitle
            ELSE
                HTMLTEXT=,JOIN("•",[titlecaps_mod]),<modifier_title_default>,,270,BE,100,modtitle
            ENDIF
        ELSE
            IF= [title_area_scale] <> ""
                HTMLTEXT=,"[titlecaps_mod]",<modifier_title>,,270,BE,100,modtitle
            ELSE
                HTMLTEXT=,"[titlecaps_mod]",<modifier_title_default>,,270,BE,100,modtitle
            ENDIF
        ENDIF
    ENDIF

    ;; Artifact overlay if applicable
    IF= ( ([card_type] = "Artifact") _OR_ ("artifact_overlay" @ {tags?§}) )
        ;;IF= ("full_portrait" # {tags?§}) _OR_ ("artifact_overlay" @ {tags?§})
            
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = ,[mockup_artifact_overlay], <card_background>, 0, PN
            ELSE
                IMAGE = ,[artifact_overlay], <card_background>, 0, PN
            ENDIF
        ;;ENDIF
        ;;RECTANGLE=,<card_background>,#00FFFF
    ENDIF

    ;; Card Type
    IF= [card_subtype] = ""
        IF= [display_type] <> ""
            HTMLTEXT=,CONCAT1("[carddisplaytypecaps]",""),<modifier_type_panel>,,0,BE,100,typeplate
        ELSE=
            HTMLTEXT=,CONCAT1("[cardtypecaps]",""),<modifier_type_panel>,,0,BE,100,typeplate
        ENDIF
    ELSE
        IF= [display_type] <> ""
            HTMLTEXT=,CONCAT1({carddisplaytypecaps?§}," • ",{cardsubtypecaps?§}),<modifier_type_panel>,,0,BE,100,typeplate
        ELSE=
            HTMLTEXT=,CONCAT1({cardtypecaps?§}," • ",{cardsubtypecaps?§}),<modifier_type_panel>,,0,BE,100,typeplate
        ENDIF
    ENDIF

    ;; Site Modifiers
    IF= [site] <> ""
        IF= "disable_site" # {tags?§}
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path],minion_site_number_mockup_position_,[card_size],.png), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],minion_site_number_position_,[card_size],.png), <card_background>, 0, PN
            ENDIF
        ENDIF
        HTMLTEXT=,"[site]",<third_icon>,,0,BE,100,numbers
    ENDIF

ELSEIF= ({template?§} = "site") _OR_ ({template?§} = "sanctuary")

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 270, PN
    ELSE
        ;; Full art means we are not using a template
        IF= "full_art" @ {tags?§}
            IF= "full_portrait" @ {tags?§}
                IF= "no_rotate" @ {tags?§}
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
                ELSE
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 270, PN
                ENDIF
            ELSE
                IF= "no_rotate" @ {tags?§}
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 0, PN
                ELSE
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 270, PN
                ENDIF
            ENDIF
        ELSE
            ;; Insert the card portrait, since this is "beneath" everything else

            ;; full portraits are intended to take up the whole card size, so
            ;; that lower res card images can just be slotted in without needing to
            ;; cut out the portrait.
            IF= "full_portrait" @ {tags?§}
                IF= "no_rotate" @ {tags?§}
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
                ELSE
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 270, PN
                ENDIF
            ELSE
                IF= "no_rotate" @ {tags?§}
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <site_card_portrait>, 0, PN
                ELSE
                    IMAGE = , JOIN([card_image_path],/,[image_name]), <site_card_portrait>, 270, PN
                ENDIF
            ENDIF

            ;; Now for the actual card template
            ;; If the culture is set, use that to look up a custom template name
            IF= [culture] <> ""
                IF= ([card_type] = "Sanctuary") _OR_ ([card_subtype] = "Sanctuary")

                    IF= [rotate_site_templates] = True
                        IMAGE = , [custom_site_sanctuary_template], <card_background>, 270, PN
                    ELSE
                        IMAGE = , [custom_site_sanctuary_template], <card_background>, 0, PN
                    ENDIF
                ELSE
                    IF= [rotate_site_templates] = True
                        IMAGE = , [custom_site_standard_template], <card_background>, 270, PN
                    ELSE
                        IMAGE = , [custom_site_standard_template], <card_background>, 0, PN
                    ENDIF
                ENDIF
            ELSE
                IF= ([card_type] = "Sanctuary") _OR_ ([card_subtype] = "Sanctuary")
                    IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                        IF= [rotate_site_templates] = True
                            IMAGE = , [mockup_site_sanctuary_template], <card_background>, 270, PN
                        ELSE
                            IMAGE = , [mockup_site_sanctuary_template], <card_background>, 0, PN
                        ENDIF
                    ELSE
                        IF= [rotate_site_templates] = True
                            IMAGE = , [site_sanctuary_template], <card_background>, 270, PN
                        ELSE
                            IMAGE = , [site_sanctuary_template], <card_background>, 0, PN
                        ENDIF
                    ENDIF

                ELSE
                    IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                        IF= [rotate_site_templates] = True
                            IMAGE = , [mockup_site_standard_template], <card_background>, 270, PN
                        ELSE
                            IMAGE = , [mockup_site_standard_template], <card_background>, 0, PN
                        ENDIF
                    ELSE
                        IF= [rotate_site_templates] = True
                            IMAGE = , [site_standard_template], <card_background>, 270, PN
                        ELSE
                            IMAGE = , [site_standard_template], <card_background>, 0, PN
                        ENDIF
                    ENDIF
                ENDIF
            ENDIF
        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        RECTANGLE=,<site_title>,#00FFFF
    ENDIF

    ;; Site Title
    HTMLTEXT=,"[title]",<site_title>,,270,BE,100,sitetitle

    ;;RECTANGLE=,<site_card_portrait>,#FF00FF

    ;;RECTANGLE=,<site_site_number_icon>,#ff00FF

    ;; Site Compass Icon / Site Number
    IF= "T" @ [site]
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]site_block_tower_mockup_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_block_tower_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
        IF= "R" @ [site]
            HTMLTEXT=,"[sitetwo]",<site_tower_site_number_icon>,,270,BE,100,numbers
        ELSE
            HTMLTEXT=,"[siteone]",<site_tower_site_number_icon>,,270,BE,100,numbers
        ENDIF
    ELSEIF= "K" @ [site]
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]site_block_king_mockup_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_block_king_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
        IF="R" @ [site]
            HTMLTEXT=,"[sitetwo]",<site_king_site_number_icon>,,270,BE,100,numbers
        ELSE
            HTMLTEXT=,"[siteone]",<site_king_site_number_icon>,,270,BE,100,numbers
        ENDIF
    ELSEIF= [site] <> ""
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]site_block_fellowship_mockup_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_block_fellowship_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
        HTMLTEXT=,"[site]",<site_site_number_icon>,,270,BE,100,numbers
    ELSE
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]site_block_shadows_mockup_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_block_shadows_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
    ENDIF


    ;; Site Arrows
    IF= "right_arrow" @ {tags?§}
        IF= ([card_type] = "Sanctuary") _OR_ ([card_subtype] = "Sanctuary")
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path]site_arrow_right_sanctuary_mockup_position_,[card_size],.png), <card_background>, 270, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],site_arrow_right_sanctuary_position_,[card_size],.png), <card_background>, 270, PN
            ENDIF
        ELSE
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path]site_arrow_right_mockup_position_,[card_size],.png), <card_background>, 270, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],site_arrow_right_position_,[card_size],.png), <card_background>, 270, PN
            ENDIF
        ENDIF
    ELSE
        IF= ([card_type] = "Sanctuary") _OR_ ([card_subtype] = "Sanctuary")
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path]site_arrow_left_sanctuary_mockup_position_,[card_size],.png), <card_background>, 270, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],site_arrow_left_sanctuary_position_,[card_size],.png), <card_background>, 270, PN
            ENDIF
        ELSE
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path]site_arrow_left_mockup_position_,[card_size],.png), <card_background>, 270, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],site_arrow_left_position_,[card_size],.png), <card_background>, 270, PN
            ENDIF
        ENDIF
    ENDIF

ELSEIF= {template?§} = "race"

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 270, PN
    ELSE
        ;; Full art means we are not using a template
        IF= "full_art" @ {tags?§}
            IF= "full_portrait" @ {tags?§}
                IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 0, PN
            ENDIF
        ELSE
            ;; Insert the card portrait, since this is "beneath" everything else

            ;; full portraits are intended to take up the whole card size, so
            ;; that lower res card images can just be slotted in without needing to
            ;; cut out the portrait.
            IF= "full_portrait" @ {tags?§}
                IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([card_image_path],/,[image_name]), <race_card_portrait>, 0, PN
            ENDIF

            ;;RECTANGLE=,<race_card_portrait>,#FF00FF

            ;; Now for the actual card template
            ;; If the culture is set, use that to look up a custom template name
            IF= [culture] <> ""
                IMAGE = , [custom_site_race_template], <card_background>, 0, PN
            ELSE
                IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                    IMAGE = , [mockup_site_race_template], <card_background>, 0, PN
                ELSE
                    IMAGE = , [site_race_template], <card_background>, 0, PN
                ENDIF
            ENDIF
        ENDIF
    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        RECTANGLE=,<race_title>,#00FFFF
    ENDIF

    ;; Site Title
    HTMLTEXT=,"[title]",<race_title>,,0,BE,100,sitetitle

    

    ;;RECTANGLE=,<race_region_icon>,#00FFFF

    ;; Site Compass Icon
    IF= "T" @ [site]
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]race_region_towers_mockup_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],race_region_towers_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
    ELSEIF= "K" @ [site]
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]race_region_king_mockup_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],race_region_king_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
    ELSEIF= [site] <> ""
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path]race_region_fellowship_mockup_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],race_region_fellowship_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
    ELSE
        ;;draw nothing if site is blank
    ENDIF


    ;; Site number text
    IF= [site] <> ""
        IF= "R" @ [site]
            HTMLTEXT=,"[sitetwo]",<race_region_icon>,,0,BE,100,numbers
        ELSE
            HTMLTEXT=,"[siteone]",<race_region_icon>,,0,BE,100,numbers
        ENDIF
    ELSE
        ;;draw nothing if site is blank
    ENDIF


ELSEIF= {template?§} = "onering"

    ;; Debug printing is used to put a full-sized card in the background for
    ;; the purposes of checking exact alignment of text, etc
    IF= [debug_print] = True
        IMAGE = , JOIN([debug_image_path],[debug_image_name]), <card_background>, 0, PN
    ELSE
        ;; full portraits are intended to take up the whole card size, so
        ;; that lower rest cards can just be slotted in without needing to
        ;; cut out the portrait.
        IF= "full_portrait" @ {tags?§}
            IMAGE = , JOIN([card_image_path],/,[image_name]), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([card_image_path],/,[image_name]), <full_card_portrait>, 0, PN
        ENDIF

    ENDIF

    ;; Debug drawing the title/subtitle area, before the drawing of the text so it remains visible
    IF= [debug_regions] = "True"
        IF= [title_area_scale] <> ""
            RECTANGLE=,<modifier_title>,#00FFFF
        ELSE
            RECTANGLE=,<modifier_title_default>,#00FFFF
        ENDIF
    ENDIF

    ;; Title
    IF=([unique] = T) _OR_ ([unique] = Y)
        IF= [title_area_scale] <> ""
            HTMLTEXT=,JOIN("•",[titlecaps_ring]),<modifier_title>,,270,BE,100,ringtitle
        ELSE
            HTMLTEXT=,JOIN("•",[titlecaps_ring]),<modifier_title_default>,,270,BE,100,ringtitle
        ENDIF
    ELSE
        IF= [title_area_scale] <> ""
            HTMLTEXT=,"[titlecaps_ring]",<modifier_title>,,270,BE,100,ringtitle
        ELSE
            HTMLTEXT=,"[titlecaps_ring]",<modifier_title_default>,,270,BE,100,ringtitle
        ENDIF
    ENDIF

    ;; Subtitle
    IF= [subtitle] <> ""
        HTMLTEXT=,"[subtitlecaps_ring]",<modifier_type_panel>,,0,BE,100,typeplate
    ENDIF
ELSEIF= {template?§} = "back"

    IMAGE = , [back_template], <card_background>, 0, PN
ELSE=
    RECTANGLE=,0,0,100%,100%,#9C2542
    TEXT=,CONCAT1("Unrecognized template type: ",{template?§}),0,0,100%,100%
ENDIF

;;RECTANGLE=,<modifier_type_panel>,#FF00FF


;; Culture Icon
IF= ("full_art" @ {tags?§}) _AND_ ("onering" # {template?§}) _AND_ ("full_portrait" # {tags?§}) _AND_ ("disable_culture" # {tags?§}) _AND_ ("site" # {template?§}) _AND_ ( ([card_type] <> "Artifact") _OR_ ("artifact_overlay" # {tags?§}) )
    
    IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
        IMAGE = , [mockup_culture_overlay], <card_background>, 0, PN
    ELSE
        IMAGE = , [culture_overlay], <card_background>, 0, PN
    ENDIF
ENDIF


IF= [twilight] <> ""
    ;; Follower Twilight Icons
    IF= ([card_type] = "Follower") _OR_ ("highlight_twilight" @ {tags?§})
        IF= "shadow" @ {tags?§}
            
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path],follower_shadow_mockup_position_,[card_size],.png), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],follower_shadow_position_,[card_size],.png), <card_background>, 0, PN
            ENDIF
        ELSE
            
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path],follower_freeps_mockup_position_,[card_size],.png), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],follower_freeps_position_,[card_size],.png), <card_background>, 0, PN
            ENDIF
        ENDIF
    ELSEIF= [template] = "site"
        ;; Twilight Cost Icon
        
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path],site_twilight_number_mockup_position_,[card_size],.png), <card_background>, 270, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],site_twilight_number_position_,[card_size],.png), <card_background>, 270, PN
        ENDIF
    ELSEIF= ("full_art" @ {tags?§}) _AND_ ("disable_twilight" # {tags?§})
        IF= ("free_peoples" @ {tags?§}) _OR_ ("shadow" # {tags?§})
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , [mockup_freeps_twilight_overlay], <card_background>, 0, PN
            ELSE
                IMAGE = , [freeps_twilight_overlay], <card_background>, 0, PN
            ENDIF
        ELSE
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , [mockup_shadow_twilight_overlay], <card_background>, 0, PN
            ELSE
                IMAGE = , [shadow_twilight_overlay], <card_background>, 0, PN
            ENDIF
        ENDIF
    ENDIF

    ;; Twilight Cost Text
    IF= ([template] = "character") _OR_ ([template] = "modifier") _OR_ ([template] = "support")
        HTMLTEXT=,"[twilight]",<twilight_cost_icon>,,0,BE,100,twilight
    ELSEIF= [template] = "site"
        HTMLTEXT=,"[twilight]",<site_twilight_cost_icon>,,270,BE,100,twilight
    ENDIF
ENDIF



;;Strength

IF= [strength] <> ""
    IF= "disable_strength" # {tags?§}
        
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path],strength_mockup_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],strength_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
    ENDIF
    HTMLTEXT=,"[strength]",<strength_icon>,,0,BE,100,numbers
ENDIF

;;Vitality
IF= [vitality] <> ""
    IF= "disable_vitality" # {tags?§}

        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path],vitality_mockup_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],vitality_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
    ENDIF
    HTMLTEXT=,"[vitality]",<vitality_icon>,,0,BE,100,numbers
ENDIF

;; Signets
IF= ([signet] <> "") _AND_ _NOT_ ("disable_signet" @ {tags?§})
    
    IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
        IMAGE = , JOIN([mockup_positioned_icon_path],signet_,[signet],_mockup_position_,[card_size],.png), <card_background>, 0, PN
    ELSE
        IMAGE = , JOIN([positioned_icon_path],signet_,[signet],_position_,[card_size],.png), <card_background>, 0, PN
    ENDIF
ENDIF

;; Resistance and ring-bearer
IF= ([card_type] <> Minion) _AND_ ([resistance] <> "")
    IF= "disable_resistance" # {tags?§}
        IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
            IMAGE = , JOIN([mockup_positioned_icon_path],resistance_mockup_position_,[card_size],.png), <card_background>, 0, PN
        ELSE
            IMAGE = , JOIN([positioned_icon_path],resistance_position_,[card_size],.png), <card_background>, 0, PN
        ENDIF
        IF= ("ring_bearer" @ {tags?§}) _OR_ ("ring-bearer" @ {tags?§})
            IF= ([force_mockup] = True) _OR_ ("mockup" @ {tags?§})
                IMAGE = , JOIN([mockup_positioned_icon_path],resistance_ring_mockup_position_,[card_size],.png), <card_background>, 0, PN
            ELSE
                IMAGE = , JOIN([positioned_icon_path],resistance_ring_position_,[card_size],.png), <card_background>, 0, PN
            ENDIF
        ENDIF
    ENDIF
    HTMLTEXT=,"[resistance]",<third_icon>,,0,BE,100,numbers
ENDIF

;;RECTANGLE=,<collectors_info>,#ff00ff
;;Collector's Info
;IF= [symrarity] <> [rarity]
;    HTMLTEXT=,JOIN([set_num]," <colltext>",[symrarity],"</colltext> ", [card_num]),<collectors_info>,,0,BE,100,collinfo
;ELSE
    HTMLTEXT=,CONCAT1({set_num?§}," ",{symrarity?§}," ",{card_num?§}),<collectors_info>,,0,BE,100,collinfo
    ;;HTMLTEXT=,JOIN([set_num]," ",[symrarity]," ",[card_num]),<collectors_info>,,0,BE,100,collinfo
;;ENDIF



;; Debug drawing the game text area, before the drawing of the text so it remains visible
IF= [debug_regions] = "True"
    IF= [template] = "site"
        IF= [game_text_width] <> ""
            RECTANGLE=,<site_game_text>,#ff00ff
        ELSE
            RECTANGLE=,<site_game_text_default>,#ff00ff
        ENDIF
    ELSEIF= [template] = "race"
        IF= [game_text_width] <> ""
            RECTANGLE=,<race_game_text>,#ff00ff
        ELSE
            RECTANGLE=,<race_game_text_default>,#ff00ff
        ENDIF
    ELSE
        IF= [game_text_width] <> ""
            RECTANGLE=,<game_text>,#ff00ff
        ELSE
            RECTANGLE=,<game_text_default>,#ff00ff
        ENDIF
    ENDIF
ENDIF


;; First combine the game text and lore into a single contiguous set of text
;; with the lore marked as a separate font.

[game_text_replaced] = CONCAT1(<start>,[game_text],</start>)
[lore_replaced] = CONCAT1(<lore>,[lore],</lore>)
[promo_replaced] = CONCAT1(<promo>,[promocaps],</promo>)

;; including this file will perform a ton of find-replaces on the text, including
;; for symbols, bolding, and smart quote handling.
INCLUDE=game_text_replacements.txt

[game_text_replaced] = REPLACE([game_text_replaced],<start>,)
[game_text_replaced] = REPLACE([game_text_replaced],</start>,)

[lore_replaced] = REPLACE([lore_replaced],<lore>,)
[lore_replaced] = REPLACE([lore_replaced],</lore>,)

[promo_replaced] = REPLACE([promo_replaced],<promo>,)
[promo_replaced] = REPLACE([promo_replaced],</promo>,)

;;[card_full_text] = CONCAT1({game_text_replaced?§},<lore>,{lore_replaced?§},</lore><promo>,{promo_replaced?§},</promo>)
[card_full_text] = JOIN([game_text_replaced],<lore>,[lore_replaced],</lore><promo>,[promo_replaced],</promo>)

[card_full_text] = REPLACE([card_full_text],<lore></lore>,)
[card_full_text] = REPLACE([card_full_text],<promo></promo>,)


;; Finally, draw the game text itself.
IF= ([template] = "character") _OR_ ([template] = "modifier") _OR_ ([template] = "support") _OR_ ([template] = "onering")
    IF= [game_text_width] <> ""
        IF= "thin_space" @ {tags?§}
            HTMLTEXT=,[card_full_text],<game_text>,,0,BE,100,thin
        ELSE
            HTMLTEXT=,[card_full_text],<game_text>,,0,BE,100,gametext
        ENDIF
    ELSE
        IF= "thin_space" @ {tags?§}
            HTMLTEXT=,[card_full_text],<game_text_default>,,0,BE,100,thin
        ELSE
            HTMLTEXT=,[card_full_text],<game_text_default>,,0,BE,100,gametext
        ENDIF
    ENDIF
ELSEIF = [template] = "site"
    IF= [game_text_width] <> ""
        IF= "thin_space" @ {tags?§}
            HTMLTEXT=,[card_full_text],<site_game_text>,,270,BE,100,thin
        ELSE
            HTMLTEXT=,[card_full_text],<site_game_text>,,270,BE,100,gametext
        ENDIF
    ELSE
        IF= "thin_space" @ {tags?§}
            HTMLTEXT=,[card_full_text],<site_game_text_default>,,270,BE,100,thin
        ELSE
            HTMLTEXT=,[card_full_text],<site_game_text_default>,,270,BE,100,gametext
        ENDIF
    ENDIF
ELSEIF = [template] = "race"
    IF= [game_text_width] <> ""
        IF= "thin_space" @ {tags?§}
            HTMLTEXT=,[card_full_text],<race_game_text>,,0,BE,100,thin
        ELSE
            HTMLTEXT=,[card_full_text],<race_game_text>,,0,BE,100,gametext
        ENDIF
    ELSE
        IF= "thin_space" @ {tags?§}
            HTMLTEXT=,[card_full_text],<race_game_text_default>,,0,BE,100,thin
        ELSE
            HTMLTEXT=,[card_full_text],<race_game_text_default>,,0,BE,100,gametext
        ENDIF
    ENDIF
ENDIF

;GRID=,<full_card_portrait>,#FF00FF,0.01,11,15
;;RECTANGLE=,<full_card_portrait>,#FF00FF


IF= "no_disclaimer" # {tags?§}
    IF= "disclaimer_light" @ {tags?§}
        IMAGE = , JOIN([positioned_icon_path],disclaimer_light_position_,[card_size],.png), <card_background>, 0, PN
    ELSEIF= ([template] = "onering") _OR_ ("disclaimer_dark" @ {tags?§}) _OR_ ("full_art" @ {tags?§})
        IMAGE = , JOIN([positioned_icon_path],disclaimer_dark_position_,[card_size],.png), <card_background>, 0, PN
    ENDIF
ENDIF


;; Drawing the custom icon if it's provided; this is intended for things like the
;; weta cricket icon, etc.

IF= [custom_icon] <> ""
    IMAGE = , [custom_icon_path], <card_background>, 0, PN
ENDIF

<errata_area> = 8.5%, 93.65%, 20%, 5%

IF= [errata_date] <> ""
    IMAGE = , JOIN([positioned_icon_path],"errata",_position_,[card_size],.png), <card_background>, 0, PN
    HTMLTEXT=,[errata_date],<errata_area>,,0,BE,100,erratatext
ENDIF



;; Draw additional borders over the top
IF= {border_color?§} <> ""
    ROUNDRECT=,<card_background>,{border_color?§},EMPTY,9%,6,9.5
ELSE
    ROUNDRECT=,<card_background>,[default_border_color],EMPTY,9%,6,9.5
ENDIF

;;ROUNDRECT=,<card_background>,#FFFFFF,EMPTY,-1%,15,15

;; Last but not least, finally write the file (if not configured to do otherwise).
IF= [auto_write_files] = True
    IF= [include_size_in_card_output] = "True"
        SAVE=,JOIN([output_path],/,[id],_,[card_size],.,[output_format]),0,0,100%,100%,,,[card_mask_path]
    ELSE
        SAVE=,JOIN([output_path],/,[id],.,[output_format]),0,0,100%,100%,,,[card_mask_path]
    ENDIF
ENDIF

